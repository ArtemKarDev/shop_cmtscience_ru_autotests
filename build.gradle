plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
}

repositories {
    mavenCentral()
}

compileTestJava {
    options.encoding = 'UTF-8'
}

def allureVersion = "2.27.0"

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
    testCompileOnly 'org.projectlombok:lombok:1.18.34'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'

    testImplementation(
            "com.codeborne:selenide:6.19.1",
            "org.junit.jupiter:junit-jupiter:5.10.0",
            'org.slf4j:slf4j-simple:2.0.7',
            'com.github.javafaker:javafaker:1.0.2',
            "io.qameta.allure:allure-selenide:${allureVersion}",
            "io.qameta.allure:allure-junit5:${allureVersion}",
            'org.projectlombok:lombok:1.18.34',
            'com.fasterxml.jackson.core:jackson-databind:2.17.2',
            'org.assertj:assertj-core:3.24.2'
    )
}

// Настройка Allure
allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true) // обработка аннотации @Step
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

tasks.withType(Test) {
    useJUnitPlatform()

    // Системные свойства
    systemProperty "allure.results.directory", "$buildDir/allure-results"
    systemProperty "selenide.headless", System.getProperty("headless", "true")
    // JUnit 5: отображать полные имена параметризованных тестов
    systemProperty 'junit.jupiter.displayname.generator.default', \
                   'org.junit.jupiter.api.DisplayNameGenerator$ReplaceUnderscores'
    jvmArgs '-Dfile.encoding=UTF-8'

    // Логирование тестов - Показывать полные имена тестов с параметрами
    testLogging {
        lifecycle {
            events "started", "passed", "skipped", "failed"
            exceptionFormat "short"
            showCauses true
            showExceptions true
            showStackTraces true
            displayGranularity = 2
        }
        // Опционально: вывод стандартных потоков только при ошибках
        debug {
            events "standardOut", "standardError"
        }
    }
    // Передача системных свойств из командной строки
    systemProperties(System.getProperties())
}


// Задача для запуска тестов с тегом "smoke"
tasks.register('smoke', Test) {
    useJUnitPlatform {
        includeTags("smoke")
    }
    description = "Запуск тестов с тегом @Tag('smoke')"
    group = "verification"
}

// Задача: регрессионные тесты
tasks.register('regress', Test) {
    useJUnitPlatform {
        includeTags "regress"
    }
    description = "Запуск тестов с тегом @Tag('regress')"
    group = "verification"
}

// Алиас для удобства: ./gradlew testAll → запускает все тесты
tasks.register('testAll', Test) {
    description = "Запуск всех тестов"
    group = "verification"
}


